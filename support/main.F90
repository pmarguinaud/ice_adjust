PROGRAM MAIN

USE XRD_GETOPTIONS
USE GETDATA_MOD
USE MODI_ICE_ADJUST
USE MODD_DIMPHYEX,   ONLY: DIMPHYEX_t
USE MODD_CST,        ONLY: CST_t, CST
USE MODD_NEB,        ONLY: NEB_t, NEB
USE MODD_RAIN_ICE_PARAM, ONLY : RAIN_ICE_PARAM_t
USE MODD_STRINGS
USE MODI_INI_CST
USE MODI_INI_NEB
USE STACK_MOD
USE OMP_LIB
USE YOMHOOK, ONLY : LHOOK, DR_HOOK
USE PARKIND1, ONLY : JPRB, JPIM


IMPLICIT NONE

INTEGER      :: KLON 
INTEGER      :: KIDIA  
INTEGER      :: KFDIA  
INTEGER      :: KLEV  
INTEGER      :: KRR  

REAL, ALLOCATABLE   :: PRHODJ         (:,:,:,:)   
REAL, ALLOCATABLE   :: PEXNREF        (:,:,:,:)   
REAL, ALLOCATABLE   :: PRHODREF       (:,:,:,:)   
REAL, ALLOCATABLE   :: PPABSM         (:,:,:,:)   
REAL, ALLOCATABLE   :: PTHT           (:,:,:,:)   
REAL, ALLOCATABLE   :: PSIGS          (:,:,:,:)   
REAL, ALLOCATABLE   :: PMFCONV        (:,:,:,:)   
REAL, ALLOCATABLE   :: PRC_MF         (:,:,:,:)   
REAL, ALLOCATABLE   :: PRI_MF         (:,:,:,:)   
REAL, ALLOCATABLE   :: PCF_MF         (:,:,:,:)   
REAL, ALLOCATABLE   :: PTHS           (:,:,:,:)   
REAL, ALLOCATABLE   :: PRS            (:,:,:,:,:) 
REAL, ALLOCATABLE   :: PSRCS          (:,:,:,:)   
REAL, ALLOCATABLE   :: PCLDFR         (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLC_HRC       (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLC_HCF       (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLI_HRI       (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLI_HCF       (:,:,:,:)   
REAL, ALLOCATABLE   :: ZRS            (:,:,:,:,:) 
REAL, ALLOCATABLE   :: ZZZ            (:,:,:,:)   
REAL, ALLOCATABLE   :: ZSIGQSAT       (:,:,:)   
REAL, ALLOCATABLE   :: ZICE_CLD_WGT   (:,:,:)   

REAL, ALLOCATABLE   :: PRS_OUT        (:,:,:,:,:) 
REAL, ALLOCATABLE   :: PSRCS_OUT      (:,:,:,:)   
REAL, ALLOCATABLE   :: PCLDFR_OUT     (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLC_HRC_OUT   (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLC_HCF_OUT   (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLI_HRI_OUT   (:,:,:,:)   
REAL, ALLOCATABLE   :: PHLI_HCF_OUT   (:,:,:,:)   

INTEGER :: NPROMA, NGPBLKS, NFLEVG
INTEGER :: IBL, JLON, JLEV, JLON1, JLON2

TYPE(DIMPHYEX_t)         :: D, D0
TYPE(RAIN_ICE_PARAM_t)   :: ICEP
INTEGER                  :: HFRAC_ICE
INTEGER                  :: HCONDENS
INTEGER                  :: HLAMBDA3 
INTEGER                  :: HBUNAME  
LOGICAL                  :: OSUBG_COND
LOGICAL                  :: OSIGMAS  
LOGICAL                  :: OCND2    
LOGICAL                  :: LMFCONV
INTEGER                  :: HSUBG_MF_PDF
REAL                     :: PTSTEP    
LOGICAL                  :: LLCHECK
INTEGER                  :: IBLOCK1, IBLOCK2
INTEGER*8                :: ISTSZ
INTEGER                  :: JBLK1, JBLK2
INTEGER                  :: NTID, ITID

REAL, ALLOCATABLE :: PSTACK(:,:)
TYPE (STACK) :: YLSTACK

REAL(KIND=8) :: TS,TE
REAL(KIND=8) :: TSC, TEC, TSD, TED, ZTC, ZTD 
INTEGER :: ITIME, NTIME
INTEGER :: IRANK, ISIZE
LOGICAL :: LLVERBOSE, LLSTAT, LLBIND
REAL (KIND=JPRB) :: ZHOOK_HANDLE


CALL INITOPTIONS ()
NGPBLKS = 296
CALL GETOPTION ("--blocks", NGPBLKS)
NPROMA = 32
CALL GETOPTION ("--nproma", NPROMA)
NFLEVG = -1
CALL GETOPTION ("--nflevg", NFLEVG)
CALL GETOPTION ("--check",  LLCHECK)
IBLOCK1 = 1
CALL GETOPTION ("--check-block-1", IBLOCK1)
IBLOCK2 = NGPBLKS
CALL GETOPTION ("--check-block-2", IBLOCK2)
CALL GETOPTION ("--stat", LLSTAT)
NTIME = 1
CALL GETOPTION ("--times", NTIME)
CALL GETOPTION ("--verbose", LLVERBOSE)
CALL GETOPTION ("--bind", LLBIND)
CALL CHECKOPTIONS ()

IRANK = 0
ISIZE = 1
IF (LLBIND) THEN
  CALL LINUX_BIND      (IRANK, ISIZE)
  CALL LINUX_BIND_DUMP (IRANK, ISIZE)
ENDIF

CALL GETDATA (NPROMA * NGPBLKS, 1_JPIM, NFLEVG, PRHODJ, PEXNREF, PRHODREF, PPABSM, PTHT, ZICE_CLD_WGT,  &
& ZSIGQSAT, PSIGS, PMFCONV, PRC_MF, PRI_MF, PCF_MF, PTHS, PRS, PSRCS, PCLDFR, PHLC_HRC, PHLC_HCF,       &
& PHLI_HRI, PHLI_HCF, ZRS, ZZZ, PRS_OUT, PSRCS_OUT, PCLDFR_OUT, PHLC_HRC_OUT, PHLC_HCF_OUT,             &
& PHLI_HRI_OUT, PHLI_HCF_OUT, LLVERBOSE)


KLEV = SIZE (PRS, 3)
KRR  = SIZE (PRS, 4)

IF (LLVERBOSE) PRINT *, " KLEV = ", KLEV, " KRR = ", KRR

PRINT *, " NPROMA = ", NPROMA, " KLEV = ", KLEV, " NGPBLKS = ", NGPBLKS

CALL INI_CST
CALL INI_NEB

! Taken from ini_rain_ice.F90; we only need these for ice_adjust.F90
ICEP%XCRIAUTI  = 0.2E-4 
ICEP%XCRIAUTC  = 0.5E-3
ICEP%XACRIAUTI = 0.06
ICEP%XBCRIAUTI = -3.5

! As provided by S. Riette, AROME specific

ICEP%XCRIAUTC  = 1.0000000000000000E-003
ICEP%XCRIAUTI  = 2.0000000000000001E-004
ICEP%XACRIAUTI = 6.2974856647312144E-002
ICEP%XBCRIAUTI = -3.3840957210994582    


HFRAC_ICE    = S_S
HCONDENS     = S_CB02
HLAMBDA3     = S_CB
HBUNAME      = S_DEPI
OSUBG_COND   = .TRUE.
OSIGMAS      = .TRUE.
OCND2        = .FALSE.
HSUBG_MF_PDF = S_TRIANGLE
PTSTEP       = 50.000000000000000    
LMFCONV      = .TRUE.

D0%NIT  = NPROMA * NGPBLKS
D0%NIB  = 1
D0%NIE  = NPROMA * NGPBLKS
D0%NJT  = 1
D0%NJB  = 1
D0%NJE  = 1
D0%NKL  = -1
D0%NKT  = KLEV
D0%NKA  = KLEV
D0%NKU  = 1
D0%NKB  = KLEV 
D0%NKE  = 1
D0%NKTB = 1
D0%NKTE = KLEV

ISTSZ = NPROMA * NGPBLKS * 20 * KLEV

ALLOCATE (PSTACK (ISTSZ, 1))

TS = OMP_GET_WTIME ()

ZTD = 0.
ZTC = 0.

IF (LHOOK) CALL DR_HOOK ('MAIN',0,ZHOOK_HANDLE)

DO ITIME = 1, NTIME

  TSD = OMP_GET_WTIME ()

!$acc data &
!$acc      & copyin  (D0, CST, ICEP, NEB, KRR, HFRAC_ICE, HCONDENS, HLAMBDA3, HBUNAME, OSUBG_COND, OSIGMAS, OCND2, HSUBG_MF_PDF, PTSTEP, LMFCONV, &
!$acc      &          ZSIGQSAT, PRHODJ, PEXNREF, PRHODREF, PSIGS, PMFCONV, PPABSM, ZZZ, PCF_MF, PRC_MF, PRI_MF, ZRS, ZICE_CLD_WGT) &
!$acc      & copy    (PRS, PTHS), &
!$acc      & copyout (PSRCS, PCLDFR, PHLC_HRC, PHLC_HCF, PHLI_HRI, PHLI_HCF) &
!$acc      & create  (PSTACK) 

  TSC = OMP_GET_WTIME ()

#ifdef USE_OPENMP
!$OMP PARALLEL PRIVATE (D, ITID, JBLK1, JBLK2)
#endif

#ifdef _OPENACC
JBLK1 = 1 
JBLK2 = NGPBLKS
#endif

#ifdef USE_OPENMP
NTID = OMP_GET_MAX_THREADS ()
ITID = OMP_GET_THREAD_NUM ()
JBLK1 = 1 +  (NGPBLKS * (ITID+0)) / NTID
JBLK2 =      (NGPBLKS * (ITID+1)) / NTID


!PRINT *, ITID, JBLK1, JBLK2

#endif

!$acc parallel loop gang vector private (YLSTACK, IBL, JLON, D) collapse (2)

  DO IBL = JBLK1, JBLK2


#ifdef _OPENACC
  DO JLON = 1, NPROMA
    D = D0
    D%NIB = (IBL - 1) * NPROMA + JLON
    D%NIE = (IBL - 1) * NPROMA + JLON
#endif

#ifdef USE_OPENMP
    D = D0
    D%NIB = (IBL - 1) * NPROMA + 1
    D%NIE =  IBL      * NPROMA
#endif

#ifdef USE_STACK
    YLSTACK%L = LOC (PSTACK (1, 1))
    YLSTACK%U = YLSTACK%L + ISTSZ * INT (KIND (PSTACK), 8)
#else
    YLSTACK%L = 0
    YLSTACK%U = 0
#endif

    CALL ICE_ADJUST (D, CST, ICEP, NEB, KRR, HFRAC_ICE, HCONDENS, HLAMBDA3, HBUNAME, OSUBG_COND,                          &
    & OSIGMAS, OCND2, HSUBG_MF_PDF, PTSTEP, ZSIGQSAT (:, :, 1), PRHODJ=PRHODJ (:, :, :, 1), PEXNREF=PEXNREF (:, :, :, 1), &
    & PRHODREF=PRHODREF (:, :, :, 1), PSIGS=PSIGS (:, :, :, 1), LMFCONV=LMFCONV, PMFCONV=PMFCONV (:, :, :, 1),            &
    & PPABST=PPABSM (:, :, :, 1), PZZ=ZZZ (:, :, :, 1), PEXN=PEXNREF (:, :, :, 1), PCF_MF=PCF_MF (:, :, :, 1),            &
    & PRC_MF=PRC_MF (:, :, :, 1), PRI_MF=PRI_MF  (:, :, :, 1), PRV=ZRS(:, :, :, 1, 1), PRC=ZRS(:, :, :, 2, 1),            &
    & PRVS=PRS(:, :, :, 1, 1), PRCS=PRS(:, :, :, 2, 1), PTH=ZRS(:, :, :, 0, 1), PTHS=PTHS (:, :, :, 1),                   &
    & PSRCS=PSRCS (:, :, :, 1), PCLDFR=PCLDFR (:, :, :, 1), PRR=ZRS(:, :, :, 3, 1), PRI=ZRS(:, :, :, 4, 1),               &
    & PRIS=PRS(:, :, :, 4, 1), PRS=ZRS(:, :, :, 5, 1), PRG=ZRS(:, :, :, 6, 1), PHLC_HRC=PHLC_HRC(:, :, :, 1),             &
    & PHLC_HCF=PHLC_HCF(:, :, :, 1), PHLI_HRI=PHLI_HRI(:, :, :, 1), PHLI_HCF=PHLI_HCF(:, :, :, 1),                        &
    & PICE_CLD_WGT=ZICE_CLD_WGT(:, :, 1) &
#ifdef USE_STACK
    & , YDSTACK=YLSTACK &
#endif
    & )

#ifdef _OPENACC
    ENDDO
#endif

  ENDDO

#ifdef USE_OPENMP
!$OMP END PARALLEL
#endif

!$acc end parallel loop

  TEC = OMP_GET_WTIME ()

!$acc end data

  TED = OMP_GET_WTIME ()

  ZTC = ZTC + (TEC - TSC)
  ZTD = ZTD + (TED - TSD)

ENDDO

IF (LHOOK) CALL DR_HOOK ('MAIN',1,ZHOOK_HANDLE)

TE = OMP_GET_WTIME()

WRITE (*,'(A,F8.2,A)') 'elapsed time : ',TE-TS,' s'
WRITE (*,'(A,F8.4,A)') '          i.e. ',1000.*(TE-TS)/(NPROMA*NGPBLKS)/NTIME,' ms/gp'

PRINT *, " ZTD = ", ZTD, ZTD / REAL (NPROMA*NGPBLKS*NTIME)
PRINT *, " ZTC = ", ZTC, ZTC / REAL (NPROMA*NGPBLKS*NTIME)


IF (LLCHECK .OR. LLSTAT) THEN
  DO IBL = IBLOCK1, IBLOCK2
    JLON1 = (IBL - 1) * NPROMA + 1
    JLON2 =  IBL      * NPROMA
    PRINT *, " IBL = ", IBL
    CALL DIFF ("PSRCS",    PSRCS_OUT    (JLON1:JLON2,:,:,1), PSRCS    (JLON1:JLON2,:,:,1))
    CALL DIFF ("PCLDFR",   PCLDFR_OUT   (JLON1:JLON2,:,:,1), PCLDFR   (JLON1:JLON2,:,:,1))
    CALL DIFF ("PHLC_HRC", PHLC_HRC_OUT (JLON1:JLON2,:,:,1), PHLC_HRC (JLON1:JLON2,:,:,1))
    CALL DIFF ("PHLC_HCF", PHLC_HCF_OUT (JLON1:JLON2,:,:,1), PHLC_HCF (JLON1:JLON2,:,:,1))
    CALL DIFF ("PHLI_HRI", PHLI_HRI_OUT (JLON1:JLON2,:,:,1), PHLI_HRI (JLON1:JLON2,:,:,1))
    CALL DIFF ("PHLI_HCF", PHLI_HCF_OUT (JLON1:JLON2,:,:,1), PHLI_HCF (JLON1:JLON2,:,:,1))
  ENDDO
ENDIF

STOP

CONTAINS

SUBROUTINE DIFF (CDNAME, PREF, POUT)

CHARACTER (LEN=*) :: CDNAME
REAL :: PREF (:,:,:)
REAL :: POUT (:,:,:)

INTEGER :: JLON, JLEV


PRINT *, CDNAME
IF (LLSTAT) THEN
  PRINT *, MINVAL (PREF), MAXVAL (PREF), SUM (PREF) / SIZE (PREF)
  PRINT *, MINVAL (POUT), MAXVAL (POUT), SUM (POUT) / SIZE (POUT)
ENDIF

IF (LLCHECK) THEN
  IF (SUM (ABS (POUT) + ABS (PREF)) > 0) THEN
  WRITE (*, '(A4)', ADVANCE='NO') ""
  DO JLON = 1, NPROMA
    WRITE (*, '("|",I12,A12)', ADVANCE='NO') JLON, ""
  ENDDO
  WRITE (*, '("|")')
  DO JLEV = 1, KLEV
    WRITE (*, '(I4)', ADVANCE='NO') JLEV
    DO JLON = 1, NPROMA
      IF (ABS (PREF (JLON, 1, JLEV)) + ABS (POUT (JLON, 1, JLEV)) == 0.) THEN
      WRITE (*, '("|",2A12)', ADVANCE='NO') "", ""
      ELSE
      WRITE (*, '("|",2E12.5)', ADVANCE='NO') PREF (JLON, 1, JLEV), POUT (JLON, 1, JLEV)
      ENDIF
    ENDDO
    WRITE (*, '("|")')
  ENDDO
  ENDIF
ENDIF

END SUBROUTINE


END
